developer=https://github.com/mastodon/mastodon
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=3.4.4
# When updating this, also update the node version in mastodon-streaming@.service
pkgrel=1
pkgdesc="Decentralized microblogging"
arch=('any')
license=("AGPL3")
# source=("${pkgname}-${pkgver}::git+https://github.com/mastodon/mastodon.git#tag=v${pkgver}")
# source=("https://github.com/mastodon/mastodon/archive/refs/tags/v${pkgver}.tar.gz")
source=("${pkgname}-v${pkgver}.tar.gz::https://codeload.github.com/mastodon/mastodon/tar.gz/refs/tags/v${pkgver}")
releasepage=('https://github.com/mastodon/mastodon/releases')
depends=(
    'ffmpeg'
    'imagemagick'
    'libidn'
    'libpqxx'
    'libxml2'
    'libxslt'
    'libyaml'
    'nodejs'
    'postgresql'
    'redis'
    'ruby-bundler'
    'protobuf'
    'zlib'
)
makedepends=(
    'gcc'
    'nvm'
    'npm'
    'yarn'
    'git'
    'python'
)
options=('!strip')
install=install
sha512sums=('0105ebebf57ec44ebe817f4f05b636793e8bf06a089683a25f6a9a60d0763f093f9aa3688577207af7446c29389869b1e82e3d313d24d83c5ea5117695b33f10')

# Use NVM
# Note: NVM_DIR must be an absolute path name without a trailing slash, otherwise
# you get some error message about "not compatible ... 'prefix' option" (don't ask me)
NODE_VERSION="--lts=erbium"

prepare() {
    . /usr/share/nvm/init-nvm.sh

    cd ${srcdir}/${pkgname}-${pkgver}
    patch -p0 < ${startdir}/rexml.patch
    NVM_DIR=$(pwd)/nvm nvm install ${NODE_VERSION}

    # Allow use of any bundler
    sed -i -e '/BUNDLED/,+1d' Gemfile.lock
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    export NVM_DIR=$(pwd)/nvm

    bundle config set deployment 'true'
    bundle config set without 'development test'
    bundle install -j$(getconf _NPROCESSORS_ONLN)

    # yarn config set ignore-engines true
    yarn install --pure-lockfile

    # We need this here otherwise asset precompilation fails. However, we need to
    # change this before deploying to something instance-specific
    deviseSecretKey=$(perl -MUBOS::Utils -e 'print UBOS::Utils::randomHex(64);')
    perl -pi -e "s!^(\s*)#\s*(config\.secret_key\s*=\s*).*\$!\1\2'$deviseSecretKey'!" config/initializers/devise.rb

    echo Running bundle exec rails assets:precompile
    RAILS_ENV=production OTP_SECRET=precompile_placeholder SECRET_KEY_BASE=precompile_placeholder bundle exec rails assets:precompile
    # Those OTP_SECRET and SECRET_KEY_BASE are from the Dockerfile

    # Make a few changes
    perl -pi -e "s!cache_dir:.*\$!cache_dir: '/var/cache/' + ENV['APPCONFIGID'],!" config/boot.rb
}

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Templates
    install -D -m0644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Config files
    mkdir -p ${pkgdir}/etc/${pkgname}

# Systemd
    install -D -m0644 ${startdir}/systemd/*.{service,target} -t ${pkgdir}/usr/lib/systemd/system/
    install -D -m0644 ${startdir}/sysusers.d/mastodon.conf   -t ${pkgdir}/usr/lib/sysusers.d/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Cache
    mkdir -p ${pkgdir}/var/cache/mastodon

# Code
    install -D -m0755 ${startdir}/bin/* -t ${pkgdir}/ubos/share/${pkgname}/bin/
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/mastodon
    cp -a ${srcdir}/${pkgname}-${pkgver}/*       ${pkgdir}/ubos/share/${pkgname}/mastodon/
    cp -a ${srcdir}/${pkgname}-${pkgver}/.bundle ${pkgdir}/ubos/share/${pkgname}/mastodon/

# Add UBOS rake tasks
    install -m644 ${startdir}/ubos.rake ${pkgdir}/ubos/share/${pkgname}/mastodon/lib/tasks/ubos.rake
}
