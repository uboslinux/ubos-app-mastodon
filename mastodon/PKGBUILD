developer=https://github.com/tootsuite/mastodon
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=2.7.4
pkgrel=1
pkgdesc="Decentralized microblogging"
arch=('any')
license=("AGPL3")
# source=("${pkgname}-${pkgver}::git+https://github.com/tootsuite/mastodon.git#tag=v${pkgver}")
source=("https://github.com/tootsuite/mastodon/archive/v${pkgver}.tar.gz")
releasepage=('https://github.com/tootsuite/mastodon/releases')
depends=(
    'ffmpeg'
    'gcc'
    'imagemagick'
    'libidn'
    'libpqxx'
    'libxml2'
    'libxslt'
    'nodejs'
    'protobuf-c'
    'redis'
    'ubos-rails-support'
)
makedepends=(
    'npm'
    'postgresql-libs'
    'yarn'
    'git'
    'python'
)
options=('!strip')
sha512sums=('d4e3d909940adde07db2dfb345704416db0c4e750e10f7901ff211b3a35658926d06ba4aa5d1d4fc9f473a15babc2a7c30571d7a880aa709e96c4206b1c806d0')
install=install

# prepare() {
#     cd ${srcdir}/${pkgname}-${pkgver}
#   patch -p0 < ${startdir}/upgrade-charlock-holmes.patch
# }

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    echo Updating Gemfile.lock
    rm Gemfile.lock
    bundle lock --update

    echo Running bundle install
    bundle install --path vendor/bundle --no-deployment
    bundle install --path vendor/bundle -j$(getconf _NPROCESSORS_ONLN) --deployment --without development test

    echo Running yarn install
    yarn config set ignore-engines true
    yarn install --pure-lockfile

    # We need this here otherwise asset precompilation fails. However, we need to
    # change this before deploying to something instance-specific
    deviseSecretKey=$(perl -MUBOS::Utils -e 'print UBOS::Utils::randomHex(64);')
    perl -pi -e "s!^(\s*)#\s*(config\.secret_key\s*=\s*).*\$!\1\2'$deviseSecretKey'!" config/initializers/devise.rb

    echo Running bundle exec
    RAILS_ENV=production OTP_SECRET=precompile_placeholder SECRET_KEY_BASE=precompile_placeholder bundle exec rails assets:precompile
    # Those OTP_SECRET and SECRET_KEY_BASE are from the Dockerfile

    # Make a few changes
    perl -pi -e "s!cache_dir:.*\$!cache_dir: '/var/cache/' + ENV['APPCONFIGID'],!" config/boot.rb
}

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Templates
    install -D -m0644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Config files
    mkdir -p ${pkgdir}/etc/${pkgname}

# Systemd
    install -D -m0644 ${startdir}/systemd/*.{service,target} -t ${pkgdir}/usr/lib/systemd/system/
    install -D -m0644 ${startdir}/sysusers.d/mastodon.conf   -t ${pkgdir}/usr/lib/sysusers.d/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Cache
    mkdir -p ${pkgdir}/var/cache/mastodon

# Code
    install -D -m0755 ${startdir}/bin/* -t ${pkgdir}/ubos/share/${pkgname}/bin/
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/mastodon
    cp -a ${srcdir}/${pkgname}-${pkgver}/*       ${pkgdir}/ubos/share/${pkgname}/mastodon/
    cp -a ${srcdir}/${pkgname}-${pkgver}/.bundle ${pkgdir}/ubos/share/${pkgname}/mastodon/

# Add UBOS rake tasks
    install -m644 ${startdir}/ubos.rake ${pkgdir}/ubos/share/${pkgname}/mastodon/lib/tasks/ubos.rake
}
