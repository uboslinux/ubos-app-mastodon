developer=https://github.com/tootsuite/mastodon
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=2.9.2
pkgrel=1
pkgdesc="Decentralized microblogging"
arch=('any')
license=("AGPL3")
# source=("${pkgname}-${pkgver}::git+https://github.com/tootsuite/mastodon.git#tag=v${pkgver}")
source=("https://github.com/tootsuite/mastodon/archive/v${pkgver}.tar.gz")
releasepage=('https://github.com/tootsuite/mastodon/releases')
depends=(
    'ffmpeg'
    'gcc'
    'imagemagick'
    'libidn'
    'libpqxx'
    'libxml2'
    'libxslt'
    'nodejs'
    'protobuf-c'
    'redis'
    'ruby-bundler'
)
makedepends=(
    'nvm'
    'npm'
    'postgresql-libs'
    'yarn'
    'git'
    'python'
)
options=('!strip')
sha512sums=('dad3eccac6366e4e3e4d2a473bcb86e58679ea4240253cc10f9cd2559b09213526fa294453c0cc60649685f463090212ba2fb2401e953022224accc79a4feb0a')
install=install

# Use NVM
# Note: NVM_DIR must be an absolute path name without a trailing slash, otherwise
# you get some error message about "not compatible ... 'prefix' option" (don't ask me)
. /usr/share/nvm/init-nvm.sh
NODE_VERSION="--lts=carbon"
# Boron does not work as of 2.7.4-2, 2019-03-19

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}

    NVM_DIR=$(pwd)/nvm nvm install ${NODE_VERSION}
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    export NVM_DIR=$(pwd)/nvm

    echo Updating Gemfile.lock
    rm Gemfile.lock
    bundle lock --update

    echo Running bundle install
    bundle install --path vendor/bundle --no-deployment
    bundle install --path vendor/bundle -j$(getconf _NPROCESSORS_ONLN) --deployment --without development test

    echo Running yarn install
    yarn config set ignore-engines true
    yarn install --pure-lockfile

    # We need this here otherwise asset precompilation fails. However, we need to
    # change this before deploying to something instance-specific
    deviseSecretKey=$(perl -MUBOS::Utils -e 'print UBOS::Utils::randomHex(64);')
    perl -pi -e "s!^(\s*)#\s*(config\.secret_key\s*=\s*).*\$!\1\2'$deviseSecretKey'!" config/initializers/devise.rb

    echo Running bundle exec
    RAILS_ENV=production OTP_SECRET=precompile_placeholder SECRET_KEY_BASE=precompile_placeholder bundle exec rails assets:precompile
    # Those OTP_SECRET and SECRET_KEY_BASE are from the Dockerfile

    # Make a few changes
    perl -pi -e "s!cache_dir:.*\$!cache_dir: '/var/cache/' + ENV['APPCONFIGID'],!" config/boot.rb
}

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Templates
    install -D -m0644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Config files
    mkdir -p ${pkgdir}/etc/${pkgname}

# Systemd
    install -D -m0644 ${startdir}/systemd/*.{service,target} -t ${pkgdir}/usr/lib/systemd/system/
    install -D -m0644 ${startdir}/sysusers.d/mastodon.conf   -t ${pkgdir}/usr/lib/sysusers.d/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Cache
    mkdir -p ${pkgdir}/var/cache/mastodon

# Code
    install -D -m0755 ${startdir}/bin/* -t ${pkgdir}/ubos/share/${pkgname}/bin/
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/mastodon
    cp -a ${srcdir}/${pkgname}-${pkgver}/*       ${pkgdir}/ubos/share/${pkgname}/mastodon/
    cp -a ${srcdir}/${pkgname}-${pkgver}/.bundle ${pkgdir}/ubos/share/${pkgname}/mastodon/

# Add UBOS rake tasks
    install -m644 ${startdir}/ubos.rake ${pkgdir}/ubos/share/${pkgname}/mastodon/lib/tasks/ubos.rake
}
