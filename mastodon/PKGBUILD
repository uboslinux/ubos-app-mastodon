developer=https://github.com/tootsuite/mastodon
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=3.1.1
# When updating this, also update the node version in mastodon-streaming@.service
pkgrel=2
pkgdesc="Decentralized microblogging"
arch=('any')
license=("AGPL3")
# source=("${pkgname}-${pkgver}::git+https://github.com/tootsuite/mastodon.git#tag=v${pkgver}")
source=("https://github.com/tootsuite/mastodon/archive/v${pkgver}.tar.gz")
releasepage=('https://github.com/tootsuite/mastodon/releases')
depends=(
    'ffmpeg'
    'imagemagick'
    'libidn'
    'libpqxx'
    'libxml2'
    'libxslt'
    'libyaml'
    'nodejs'
    'postgresql'
    'redis'
    'ruby-bundler'
    'protobuf'
    'zlib'
)
makedepends=(
    'gcc'
    'nvm'
    'npm'
    'yarn'
    'git'
    'python'
)
options=('!strip')
sha512sums=('085b546c34ef86dc234fc4ad1ed96b01af8c68cdb616250ea1843c6d27c1db97735cd5b4f6674a6844cc45a81b0d25b3b9da63459c60f2b5c891d02f409eaecf')
install=install

# Use NVM
# Note: NVM_DIR must be an absolute path name without a trailing slash, otherwise
# you get some error message about "not compatible ... 'prefix' option" (don't ask me)
. /usr/share/nvm/init-nvm.sh
NODE_VERSION="--lts=carbon"
# Boron does not work as of 2.7.4-2, 2019-03-19

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}

    NVM_DIR=$(pwd)/nvm nvm install ${NODE_VERSION}
}

build() {
    # Determine version outside of directory within Gemfile.lock
    bundleVer=$(bundle --version | cut -d' '  -f3)
    cd ${srcdir}/${pkgname}-${pkgver}

    export NVM_DIR=$(pwd)/nvm

    echo Updating Gemfile.lock
    sed -i "s/1.17.3/$bundleVer/g" Gemfile.lock
    # sed -i "s/fuubar (2\.4\.0)/fuubar (2\.4\.1)/" Gemfile.lock

    echo Running bundle install
    bundle install --path vendor/bundle --no-deployment
    bundle install --path vendor/bundle -j$(getconf _NPROCESSORS_ONLN) --deployment --without development test

    echo Running yarn install
    yarn config set ignore-engines true
    yarn install --pure-lockfile

    # We need this here otherwise asset precompilation fails. However, we need to
    # change this before deploying to something instance-specific
    deviseSecretKey=$(perl -MUBOS::Utils -e 'print UBOS::Utils::randomHex(64);')
    perl -pi -e "s!^(\s*)#\s*(config\.secret_key\s*=\s*).*\$!\1\2'$deviseSecretKey'!" config/initializers/devise.rb

    echo Running bundle exec
    RAILS_ENV=production OTP_SECRET=precompile_placeholder SECRET_KEY_BASE=precompile_placeholder bundle exec rails assets:precompile
    # Those OTP_SECRET and SECRET_KEY_BASE are from the Dockerfile

    # Make a few changes
    perl -pi -e "s!cache_dir:.*\$!cache_dir: '/var/cache/' + ENV['APPCONFIGID'],!" config/boot.rb
}

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Templates
    install -D -m0644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Config files
    mkdir -p ${pkgdir}/etc/${pkgname}

# Systemd
    install -D -m0644 ${startdir}/systemd/*.{service,target} -t ${pkgdir}/usr/lib/systemd/system/
    install -D -m0644 ${startdir}/sysusers.d/mastodon.conf   -t ${pkgdir}/usr/lib/sysusers.d/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Cache
    mkdir -p ${pkgdir}/var/cache/mastodon

# Code
    install -D -m0755 ${startdir}/bin/* -t ${pkgdir}/ubos/share/${pkgname}/bin/
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/mastodon
    cp -a ${srcdir}/${pkgname}-${pkgver}/*       ${pkgdir}/ubos/share/${pkgname}/mastodon/
    cp -a ${srcdir}/${pkgname}-${pkgver}/.bundle ${pkgdir}/ubos/share/${pkgname}/mastodon/

# Add UBOS rake tasks
    install -m644 ${startdir}/ubos.rake ${pkgdir}/ubos/share/${pkgname}/mastodon/lib/tasks/ubos.rake
}
